on load:
    set {_list::*} to {skripts::*}
    if {_list::*} contains "bck_backpack.sk":
        send "§6[§4BCK§6]§r Loading backpack..." to console
        send "§6[§4BCK§6/§2Backpack§6]§r Loaded successfully !" to console
        load yaml "plugins/Skript/scripts/BCK/manager/backpack/config.yml" as "bck.manager.backpack.config"
        load yaml "plugins/Skript/scripts/BCK/manager/backpack/data.yml" as "bck.manager.backpack.data"
    else:
        broadcast "§6[§4BCK§6/§2Backpack§6]§r Script loading cancel It require (bck_manager.sk)"
        unload script file "bck_backpack.sk"
command /backpack [<number>]:
    trigger:
        set {_tier} to arg-1
        if {_tier} isn't set:
            set {_tier} to 1
        set {_i} to yaml value "data.item" from "bck.manager.backpack.config"
        set {_n} to backpackConfig("data.defaultName")
        if {_tier} >= 6:
            set {_xx} to backpackConfig("data.tierU")
            set {_t} to "%{_xx}% §46"
        else if {_tier} > 3:
            set {_xx} to backpackConfig("data.tierU")
            set {_t} to "%{_xx}% §b%{_tier}%"
        else:
            set {_t} to backpackConfig("data.tier%{_tier}%")
        set {_i} to {_i} parsed as item
        set {_i} to {_i} named {_n}
        set line 1 of lore of {_i} to backpackConfig("data.lore")
        set line 3 of lore of {_i} to {_t}
        set line 5 of lore of {_i} to "§7%randomUUID()%"
        give {_i} to player
function randomUUID() :: text:
    add "a", "b", "c", "d", "e", "f", "0", "1", "2", "3", "4", "5", "6", "7", "8" and "9" to {_chars::*}
    set {_uuid} to a random element out of {_chars::*}
    loop 35 times:
        if loop-number = 8, 13, 18 or 23:
            set {_uuid} to "%{_uuid}%-"
        else:
            set {_uuid} to "%{_uuid}%%a random element out of {_chars::*}%"
    return {_uuid}
function backpackGui(player: player, tier: number=1):
    set {_i} to {_player}'s tool
    set {_n} to display name of {_i}
    if "%{_n}%" is "Backpack":
        set {_gn} to backpackConfig("data.defaultName")
        set {_gn} to "%{_gn}%" 
    else:
        set {_gn} to "§f%{_n}%"

    set {_gui} to genericGui({_gn}, {_tier})
    set {_inv::*} to yaml list "data.inventory.%{_player}'s uuid%.%backpackGetUUID({_i})%" from "bck.manager.backpack.data"
    if {_inv::*} isn't empty:
        loop {_inv::*}:
            add loop-value to {_gui}
        set yaml list "data.inventory.%{_player}'s uuid%.%backpackGetUUID({_i})%" from "bck.manager.backpack.data" to {_none::*}
        save yaml "bck.manager.backpack.data"
    open {_gui} to {_player}
    userData({_player}, "data.inv", "set", "bck.manager.backpack.main")
function backpackGuiSaver(player: player, gui: inventory):
    set {_i} to {_player}'s tool
    set {_storage::*} to items of {_gui}
    set yaml list "data.inventory.%{_player}'s uuid%.%backpackGetUUID({_i})%" from "bck.manager.backpack.data" to {_storage::*}
    save yaml "bck.manager.backpack.data"
function backpackGetTier(item: item) :: number:
    set {_l} to line 3 of {_item}'s lore
    set {_l} to uncolored {_l}
    replace "⇾ Tier: " with "" in {_l}
    set {_l} to {_l} parsed as number
    return {_l}
function backpackGetUUID(item: item) :: string:
    set {_l} to line 5 of {_item}'s lore
    set {_l} to uncolored {_l}
    return {_l}
on click:
    set {_i} to player's tool
    set {_lore::*} to uncolored lore of {_i}
    set {_ii} to yaml value "data.item" from "bck.manager.backpack.config"
    set {_ii} to {_ii} parsed as item
    if {_i} is {_ii}:
        if "%{_lore::*}%" contains "Backpack":
            backpackGui(player, backpackGetTier({_i}))
on place:
    set {_i} to player's tool
    set {_lore::*} to uncolored lore of {_i}
    set {_ii} to yaml value "data.item" from "bck.manager.backpack.config"
    set {_ii} to {_ii} parsed as item
    if {_i} is {_ii}:
        if "%{_lore::*}%" contains "Backpack":
            cancel event
            backpackGui(player, backpackGetTier({_i}))
function backpackConfig(find: text, mode: text="extract", data: text="") :: text:
    if {_mode} is "extract":
        set {_result} to yaml value "%{_find}%" from "bck.manager.backpack.config"
        if {_result} isn't set:
            if {_data} isn't empty:
                set {_result} to {_data}
    else if {_mode} is "delete":
        set {_result} to yaml value "%{_find}%" from "bck.manager.backpack.config"
        if {_result} is set:
            delete yaml value "%{_find}%" from "bck.manager.backpack.config"
            save yaml "bck.manager.backpack.config"
        else:
            set yaml value "%{_find}%" from "bck.manager.backpack.config" to {_data}
            save yaml "bck.manager.backpack.config"
    else if {_mode} is "set":
        set yaml value "%{_find}%" from "bck.manager.backpack.config" to {_data}
        save yaml "bck.manager.backpack.config"
    return "%{_result}%"